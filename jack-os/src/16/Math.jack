// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/12/Math.jack

/**
 * A library of commonly used mathematical functions.
 * Note: Jack compilers implement multiplication and division using OS method calls.
 */
class Math {
    static Array masks_, pows_;
    static int INT_MIN, INT_MAX, HALF_INT_MAX;

    /** Initializes the library. */
    function void init() {
        var int i, mask;

        let masks_ = Array.new(15);
        let pows_ = Array.new(15);
        let i = 0;
        let mask = 1;
        while (i < 15) {
            let masks_[i] = mask;
            let i = i + 1;
            let mask = mask + mask;
        }
        let INT_MIN = -16384;
        let INT_MIN = INT_MIN + INT_MIN;
        let INT_MAX = 32767;
        let HALF_INT_MAX = 16383;
        return;
    }

    /** Returns the absolute value of x. */
    function int abs(int x) {
        if (x < 0) {
            return -x;
        }
        return x;
    }

    /** Returns the product of x and y. 
     *  When a Jack compiler detects the multiplication operator '*' in the 
     *  program's code, it handles it by invoking this method. In other words,
     *  the Jack expressions x*y and multiply(x,y) return the same value.
     */
    function int multiply(int x, int y) {
        var int mask, ret;
        var bool neg;

        let neg = false;
        if (x < 0) {
            let x = -x;
            let neg = ~neg;
        }
        if (y < 0) {
            let y = -y;
            let neg = ~neg;
        }

        let mask = 1;
        let ret = 0;
        while (mask > 0) {  // Shift until MSB is set, which means mask < 0
            if (~((mask & y) = 0)) {
                let ret = ret + x;
            }

            let x = x + x;
            let mask = mask + mask;
        }

        if (neg) {
            return -ret;
        }
        return ret;
    }

    /** Returns the integer part of x/y.
     *  When a Jack compiler detects the multiplication operator '/' in the 
     *  program's code, it handles it by invoking this method. In other words,
     *  the Jack expressions x/y and divide(x,y) return the same value.
     */
    function int divide(int x, int y) {
        var bool neg, check, cont;
        var int ret, i;

        if (y = 0) {
            do Sys.error(3);
            return 0;
        }

        let neg = false;
        let check = false;
        if (x < 0) {
            let x = -x;
            let neg = ~neg;
        }
        if (y < 0) {
            let y = -y;
            let neg = ~neg;
        }

        if (y = INT_MIN) {
            if (x = INT_MIN) {
                return 1;
            }
            return 0;
        }
        if (x = INT_MIN) {
            if (y = 1) {
                return x;
            }
            let x = INT_MAX;
            let check = true;
        }

        let ret = y;
        let i = 0;
        let cont = true;
        while ((i < 15) & cont) {
            let pows_[i] = ret;
            let i = i + 1;
            if (ret > HALF_INT_MAX) {
                let cont = false;
            }
            let ret = ret + ret;
        }

        let ret = 0;
        let i = i - 1;
        while (i > -1) {
            if (~(x < pows_[i])) {
                let ret = ret | masks_[i];
                let x = x - pows_[i];
            }
            let i = i - 1;
        }

        if (check & (x + 1 = y)) {
            let ret = ret + 1;
        }

        if (neg) {
            return -ret;
        }
        return ret;
    }

    /** Returns the integer part of the square root of x. */
    function int sqrt(int x) {
        var int i, ret, cur, sq;

        if (x < 0) {
            do Sys.error(4);
            return 0;
        }

        let i = 14;
        let ret = 0;
        while (~(i < 0)) {
            let cur = ret | masks_[i];
            let sq = cur * cur;
            if (~((sq < cur) | (sq > x) | (sq < 0))) {
                let ret = cur;
            }
            let i = i - 1;
        }

        return ret;
    }

    /** Returns the greater number. */
    function int max(int a, int b) {
        if (a > b) {
            return a;
        }
        return b;
    }

    /** Returns the smaller number. */
    function int min(int a, int b) {
        if (a < b) {
            return a;
        }
        return b;
    }
}
